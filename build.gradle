buildscript {
	ext {
		springBootVersion = '1.1.4.RELEASE'
	}
    repositories {
    	mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'

jar {
    baseName = 'reparaciones'
    version =  '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    
    // Fixed: use Tomacat cause starter-jetty:1.1.1 not load javax.servlet.Filter 
    compile("org.springframework.boot:spring-boot-starter-jetty")

    compile("org.springframework.boot:spring-boot-starter-actuator")
    
    compile ("org.springframework.hateoas:spring-hateoas")
    
    //Needed by spring-hateoas @EnableHypermediaSupport annotation
    compile ("org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE")
    
   // compile ("org.springframework.data:spring-data-commons-core:1.4.1.RELEASE")
    
   // compile ("org.springframework.data:spring-data-commons:1.8.0.RELEASE")
   
    compile ("com.google.guava:guava:17.0")
    
    // Required by Traverson
    compile("com.jayway.jsonpath:json-path:0.9.1")
    
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

sourceSets {
    integTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn integTest

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
